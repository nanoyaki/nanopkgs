# SPDX-SnippetCopyrightText: 2023 Balthazar Patiachvili <ratcornu+programmation@skaven.org>
# SPDX-FileCopyrightText: 2025 Hana Kretzer <hanakretzer@gmail.com>
#
# SPDX-License-Identifier: MIT
{
  lib,
  stdenvNoCC,
  zip,
  makeWrapper,
  gradle_8,
  copyDesktopItems,
  jdk21_headless,
  suwayomi-webui,
  _experimental-update-script-combinators,
  nix-update-script,
  writeShellScript,
  nixosTests,
  electron,
  makeDesktopItem,

  jdk ? jdk21_headless,
  webui ? suwayomi-webui,
  asApplication ? false,

  _sources,
  _versions,
}:

let
  self = stdenvNoCC.mkDerivation (finalAttrs: {
    inherit (_sources.suwayomi-server) pname src;
    version =
      (lib.concatStringsSep "." (lib.take 2 (lib.splitString "." _versions.suwayomi-server.version)))
      + ".${_versions.suwayomi-server.revision}";

    postPatch = ''
      echo 'const val MainClass = "suwayomi.tachidesk.MainKt"
      val getTachideskVersion = { "v${finalAttrs.version}" }
      val webUIRevisionTag = "r${webui.revision}"
      val getTachideskRevision = { "r${lib.versions.patch finalAttrs.version}" }
      ' > buildSrc/src/main/kotlin/Constants.kt

      zip -9 -r server/src/main/resources/WebUI.zip ${webui}
    '';

    nativeBuildInputs = [
      zip
      makeWrapper
      gradle_8
    ]
    ++ lib.optional asApplication copyDesktopItems;

    mitmCache = gradle_8.fetchDeps {
      pkg = self;
      data = ./deps.json;
    };

    gradleFlags = [
      # Disable download of the webui
      "-x"
      ":server:processResources"

      "-Dorg.gradle.java.home=${jdk}"
      "-Dorg.gradle.jvmargs=-Xmx2G"
    ];

    gradleBuildTask = "shadowJar";

    installPhase = ''
      runHook preInstall

      mkdir -p $out/{bin,share/suwayomi-server,share/icons/hicolor/128x128/apps}
      cp server/build/Suwayomi-Server-v${finalAttrs.version}.jar $out/share/suwayomi-server

      # Use nixpkgs suwayomi-webui and disable auto download and update
      makeWrapper ${lib.getExe jdk} $out/bin/tachidesk-server \
        --add-flags "-Dsuwayomi.tachidesk.config.server.webUIFlavor=Custom" \
        --add-flags "-Dsuwayomi.tachidesk.config.server.webUIChannel=BUNDLED" \
        --add-flags "-Dsuwayomi.tachidesk.config.server.webUIUpdateCheckInterval=0" \
    ''
    + lib.optionalString asApplication ''
      --add-flags "-Dsuwayomi.tachidesk.config.server.webUIInterface=electron" \
      --add-flags '-Dsuwayomi.tachidesk.config.server.electronPath="${lib.getExe electron}"' \
    ''
    + lib.optionalString (!asApplication) ''
      --add-flags "-Dsuwayomi.tachidesk.config.server.initialOpenInBrowserEnabled=false" \
    ''
    + ''
        --add-flags "-jar $out/share/suwayomi-server/Suwayomi-Server-v${finalAttrs.version}.jar"

      install -m644 server/build/generated/src/main/resources/server-reference.conf \
        $out/share/suwayomi-server/server.conf
      install -m644 server/src/main/resources/icon/faviconlogo-128.png \
        $out/share/icons/hicolor/128x128/apps/suwayomi-server.png

      runHook postInstall
    '';

    desktopItems = lib.optional asApplication (
      makeDesktopItem (
        with finalAttrs;

        {
          name = pname;
          desktopName = "Suwayomi Server";
          comment = "Free and open source manga reader";
          exec = meta.mainProgram;
          terminal = false;
          icon = pname;
          startupWMClass = pname;
          categories = [ "Utility" ];
        }
      )
    );

    passthru = {
      updateScript = _experimental-update-script-combinators.sequence [
        (nix-update-script { })

        {
          command = writeShellScript "update-deps.sh" ''
            $(nix-build -A suwayomi-server.mitmCache.updateScript)
          '';
        }
      ];

      tests = {
        suwayomi-server-with-auth = nixosTests.suwayomi-server.with-auth;
        suwayomi-server-without-auth = nixosTests.suwayomi-server.without-auth;
      };
    };

    meta = {
      description = "Free and open source manga reader server that runs extensions built for Mihon (Tachiyomi)";
      longDescription = ''
        Suwayomi is an independent Mihon (Tachiyomi) compatible software and is not a Fork of Mihon (Tachiyomi).

        Suwayomi-Server is as multi-platform as you can get.
        Any platform that runs java and/or has a modern browser can run it.
        This includes Windows, Linux, macOS, chrome OS, etc.
      '';
      homepage = "https://github.com/Suwayomi/Suwayomi-Server";
      downloadPage = "https://github.com/Suwayomi/Suwayomi-Server/releases";
      changelog = "https://github.com/Suwayomi/Suwayomi-Server/releases/tag/v${finalAttrs.version}";
      license = lib.licenses.mpl20;
      inherit (jdk.meta) platforms;
      sourceProvenance = with lib.sourceTypes; [
        fromSource
        binaryBytecode
      ];
      maintainers = with lib.maintainers; [
        ratcornu
        nanoyaki
      ];
      mainProgram = "tachidesk-server";
    };
  });
in
self
